apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'kotlin'

group = 'com.github.octarine-noise'
version = "2.1.7"
archivesBaseName = rootProject.name + '-MC1.10.2'

buildscript {
    ext.kotlin_version = '1.1.1'
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
configurations {
    shade
    compile.extendsFrom shade
}
dependencies {
    shade "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    shade "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}
minecraft {
    version = '1.10.2-12.18.2.2121'
    mappings = 'snapshot_20160905'
    runDir = 'run'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    into "${buildDir}/classes/main"
}

jar {
    manifest {
        attributes "FMLCorePlugin": "mods.betterfoliage.loader.BetterFoliageLoader"
        attributes "FMLCorePluginContainsFMLMod": "mods.betterfoliage.BetterFoliageMod"
        attributes "FMLAT": "BetterFoliage_at.cfg"
    }
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }
    exclude "optifine"
}

repositories {
    mavenCentral()
}